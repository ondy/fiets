import java.io.ByteArrayOutputStream
import java.util.Properties

group = 'ondy'

apply plugin: 'java'
apply plugin: 'eclipse'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}
sourceSets {
  main {
    resources {
      srcDirs = ["src/main/resources", "$buildDir/generated-resources"]
      includes = ["**/*.html","**/*.xml","**/*.properties"]
    }
  }
}

def runGitCommand(List<String> args) {
  try {
    def stdout = new ByteArrayOutputStream()
    def result = project.exec {
      commandLine(["git"] + args)
      standardOutput = stdout
      ignoreExitValue true
    }
    if (result.exitValue == 0) {
      return stdout.toString().trim()
    }
  } catch (Exception ignored) {
  }
  return ""
}

task generateDeploymentInfo {
  def outputDir = file("$buildDir/generated-resources/fiets")
  outputs.file("$outputDir/deployment-info.properties")
  doLast {
    def branch = runGitCommand(["rev-parse", "--abbrev-ref", "HEAD"])
    def commitTime = runGitCommand([
      "log", "-1", "--format=%cd", "--date=format:%Y-%m-%d %H:%M:%S %z"
    ])
    def props = new Properties()
    props.setProperty("branch", branch ?: "")
    props.setProperty("commitTime", commitTime ?: "")
    outputDir.mkdirs()
    def target = new File(outputDir, "deployment-info.properties")
    target.withOutputStream { out ->
      props.store(out, "Generated by Gradle")
    }
  }
}

processResources.dependsOn(generateDeploymentInfo)

task downloadAssets {
  doLast {
    def bootstrap = 'https://cdn.jsdelivr.net/npm/bootstrap@5.3.3/dist'
    ant.mkdir(dir: "build/assets")
    ant.get(dest: "build/assets/bootstrap.min.css",
      src: "${bootstrap}/css/bootstrap.min.css")
    ant.get(dest: "build/assets/bootstrap.min.css.map",
      src: "${bootstrap}/css/bootstrap.min.css.map")
    ant.get(dest: "build/assets/bootstrap.bundle.min.js",
      src: "${bootstrap}/js/bootstrap.bundle.min.js")
    ant.get(dest: "build/assets/bootstrap.bundle.min.js.map",
      src: "${bootstrap}/js/bootstrap.bundle.min.js.map")
    ant.get(dest: "build/assets/jquery.min.js",
      src: "https://code.jquery.com/jquery-3.7.1.min.js")
  }
}
assemble.dependsOn downloadAssets

jar {
  archiveBaseName.set('fiets')
  archiveVersion.set('0.11.0')
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  manifest {
    attributes(
      "Main-Class": "fiets.Server",
      "Multi-Release": "true"
    )
  }

  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }
  from('build/assets') {
    into 'META-INF/resources/static'
  }
  from('src/main/resources/static') {
    into 'META-INF/resources/static'
  }
}

repositories {
  mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

dependencies {
  implementation "org.apache.logging.log4j:log4j-core:2.24.3"
  implementation "org.apache.logging.log4j:log4j-api:2.24.3"
  implementation "org.jodd:jodd-lagarto:6.2.2"
  implementation "org.jodd:jodd-http:6.2.2"
  implementation "org.jodd:jodd-json:6.2.2"
  implementation "com.h2database:h2:1.4.200"
  implementation "org.nanohttpd:nanohttpd:2.3.1"
  implementation "org.jsoup:jsoup:1.18.1"
  implementation "com.syncthemall:boilerpipe:1.2.0"
  testImplementation "junit:junit:4.13.2"
}